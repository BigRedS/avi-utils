#! /usr/bin/perl

# activesites

# Finds active and inactive sites configured on the local machine. 
#
# Requires a script which returns a list of configured domain names, such 
# as my 'websites' script which is probably in this directory, but should be 
# configured here:
my $websitesScript = "./websites";
#
# Iterates through the output of my the above script and runs a DNS lookup 
# on each to see if they point at any IP address configured on the system.
# Then returns a list of these domains, categorised into "points here", 
# "doesn't point here" and "error", with Net::DNS's error string if 
# applicable

# Written by Avi (@avi.co) in 2011

# This is free software. It is distributed under the 
# terms of the 2-clause BSD license. See ./license.txt.

use strict;
use warnings;
use Net::DNS;

my $ifconfig = "/sbin/ifconfig";

# Some arrays for input and output. IPs are stored as a hash so I can easily
# grep it.
my(@inactive,@active,@error);
my %ips = &getIPs;
my %sites = &getConfiguredSites;

my $resolver = Net::DNS::Resolver->new;

foreach(keys(%sites)){
	my $site = $_;
	my $query = $resolver->search($site);
	if ($query){
		foreach my $rr ($query->answer){
			next unless $rr->type eq "A";
			my $Arecord = $rr->address;
			if($ips{$Arecord}){
				push(@active,$site);
			}else{
				push(@inactive,[$site, $Arecord]);
			}
		}
	}else{
		push(@error,[$site, $resolver->errorstring]);
	}
}

print "Point here:\n";
foreach(@active){
	print "\t$_\n";
}
print "Don't point here:\n";
foreach(@inactive){
	print "\t$$_[0]\t($$_[1])\n";
}
print "Errored:\n";
foreach(@error){
	print "\t$$_[0]\t($$_[1])\n";
}

sub getIPs() {
	my %ips;
	my $ifconfigCMD = "$ifconfig -a";
	foreach(`$ifconfigCMD`){
		if (/inet addr:(\S+)/){
			my $ip = $1;
			$ips{$ip} = ":)";
		}
	}
	return %ips;
}

sub getConfiguredSites(){
	my $websitesScriptError = "I need Avi's 'websites' katflap script.\nI'm currently looking for it at $websitesScript\nYou can probably get it by doing:\n\nwget https://github.com/BigRedS/work/raw/master/katflap/websites -O$websitesScript && chmod +x $websitesScript\n\nThe path is configured at approximately line 9";
	unless (-x $websitesScript){
		print $websitesScriptError;
		exit 1;
	}
	my %sites;
	foreach(`$websitesScript`){
		if (/.*\:\s*(.+)\n/){
			my $site = $1;
			$sites{$site} = ":)";
		}
	}
	return %sites;
}
